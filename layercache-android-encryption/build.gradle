/*
 * Copyright 2020 Appmattus Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
//apply from: "$rootDir/jacoco-android.gradle"
apply from: "$rootDir/gradle/scripts/bintray.gradle.kts"
apply from: "$rootDir/dokka-android.gradle"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.appmattus.layercache.AndroidXJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            multiDexEnabled true
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), "$rootDir/proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testBuildType "debug"
}

tasks.withType(Test) {
    systemProperty "kotlinx.coroutines.blocking.checker", "disable"
}

dependencies {
    api project(':layercache-android')
    compileOnly "androidx.annotation:annotation:1.1.0"

    implementation "androidx.preference:preference-ktx:1.1.1"

    testImplementation project(':testutils')
    testImplementation "org.robolectric:robolectric:4.3.1"

    androidTestImplementation project(':testutils')

    androidTestImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    androidTestImplementation('androidx.test:runner:1.2.0')
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'androidx.annotation', module: 'annotation'
    })
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
